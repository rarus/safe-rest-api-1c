///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2025, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	кв_ОбщегоНазначения.ЗаписатьОперациюБизнесСтатистики("ЖурналВходящихHTTPЗапросов");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОткрытьЖурналКонфликтов(Команда)
	
	ПараметрыФормы = Новый Структура("ИзменятьСоставСтрок", Элементы.Список.ИзменятьСоставСтрок);
	ОткрытьФорму("РегистрСведений.кв_ЖурналКонфликтовHTTPЗапросов.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьИзменения(Команда)
	
	Если Элементы.Список.ИзменятьСоставСтрок Тогда
		ПослеЗакрытияВопросНаИзменение(КодВозвратаДиалога.Да, Неопределено);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Изменения в журнале могут привести к ошибкам работы подсистемы.
                         |Вы уверены в продолжении?'");
	Оповещение   = Новый ОписаниеОповещения("ПослеЗакрытияВопросНаИзменение", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , НСтр("ru = 'Разрешить изменения?'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросНаИзменение(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет
		Или Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.ИзменятьСоставСтрок        = НЕ Элементы.Список.ИзменятьСоставСтрок;
	Элементы.СписокРазрешитьИзменения.Пометка  = Элементы.Список.ИзменятьСоставСтрок;
	Элементы.СписокСократитьЖурнал.Доступность = Элементы.Список.ИзменятьСоставСтрок;
	
	Элементы.Список.ТолькоПросмотр             = НЕ Элементы.Список.ИзменятьСоставСтрок;
	
КонецПроцедуры

&НаКлиенте
Процедура СократитьЖурнал(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВводаДаты", ЭтотОбъект);
	
	НаДату = НачалоДня(ТекущаяДата() - 7 * 24 * 60 * 60);
	ПоказатьВводДаты(Оповещение, НаДату, НСтр("ru = 'Удалить до:'"), ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВводаДаты(НаДату, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НаДату) Тогда
		Возврат;
	КонецЕсли;
	
	СократитьЖурналНаСервере(НаДату);
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.кв_ЖурналВходящихHTTPЗапросов"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СократитьЖурналНаСервере(НаДату)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	кв_ЖурналВходящихHTTPЗапросов.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.кв_ЖурналВходящихHTTPЗапросов КАК кв_ЖурналВходящихHTTPЗапросов
	|ГДЕ
	|	кв_ЖурналВходящихHTTPЗапросов.ДатаЗапроса < &НаДату";
	
	НачатьТранзакцию();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.кв_ЖурналВходящихHTTPЗапросов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Идентификатор.Установить(Выборка.Идентификатор);
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			
			Причина = ИнформацияОбОшибке();
			кв_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(Причина));
			
			ОтменитьТранзакцию();
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОПрограмме(Команда)
	
	ОткрытьФорму("Справочник.кв_Сервисы.Форма.ОПрограмме",, ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти
